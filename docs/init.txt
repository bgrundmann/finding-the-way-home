def completeCut N deck
  doc Cut the top N cards of deck to the bottom
  temp t
  cut N deck t
  turnover t
  turnover deck
  cut N t deck
  turnover deck
end
def cutUnder N deck pile
  doc Cut the top N cards of deck and put then under pile
  temp t
  cut N deck t
  turnover t
  turnover pile
  cut N t pile
  turnover pile
end
def deal a b
  doc Deal 1 card from a to b
  cut 1 a b
end
def deal N a b
  doc Deal N cards from a to b
  repeat N
    deal a b
  end
end
def biddle N M left right
  doc Do the biddle move, counting N cards from right to left, than stealing back M cards under right and finally counting one last card from right to left
  deal N right left
  cutUnder M left right
  deal 1 right left
end
def biddle N left right
  doc Do the biddle move, counting N+1 cards from right to left, but stealing the nth card back under right.
  biddle N 1 left right
end
def deal N deck a b
  doc Deal N cards each from deck to a and b
  repeat N
    deal 1 deck a
    deal 1 deck b
  end
end
def studDeal deck table
  doc Studdeal 1 card from deck to table
  temp t
  deal 1 deck t
  turnover t
  deal 1 t table
end
def studDeal N deck table
  doc Studdeal N cards from deck to table
  temp t
  repeat N
    studDeal deck table
  end
end
def bottomDeal deck table
  doc Bottom deal a card from deck to table
  turnover deck
  studDeal 1 deck table
  turnover deck
end
def faro N deck other
  doc Faro N cards from other into the top of deck, top of other is the new top
  temp t t1
  repeat N
    studDeal 1 other t
    studDeal 1 deck t
  end
  turnover t
  cut N t t1
  cut N t deck
  cut N t1 deck
end
def outFaro52 deck
  doc Perfect out faro of a deck of 52
  temp upper
  cut 26 deck upper
  faro 26 deck upper
end
def mnemonicaFromPhoenix deck
  doc Shuffle deck from Phoenix new deck order into mnemonica
  temp upper
  repeat 4
    outFaro52 deck
  end
  deal 26 deck upper
  cut 26 deck upper
  cut 52 upper deck 
  cut 18 deck upper
  faro 18 deck upper
  completeCut 9 deck 
end
def turnoverTopCard pile
  temp t
  cut 1 pile t
  turnover t
  cut 1 t pile
end
def underCut N deck pile
  doc Cut the bottom N cards of deck and put them on top of pile
  temp t
  turnover deck
  cut N deck t
  turnover t
  turnover deck
  cut N t pile
end
def tantalizer52 deck cardician spectator
  doc Perform the tantalizer with a deck of 52 cards
  def take N
    doc Take the N cards in the cardician pile back into the hand
    cut N cardician deck
  end
  def to 
    doc Deal the remaining card to the spectator
    deal 1 deck spectator
  end
  def toAndFro N
    doc Deal N cards each to spectator and cardician
    deal N deck spectator cardician
  end
  toAndFro 26
  take 26
  toAndFro 13
  take 13
  toAndFro 6
  to 
  take 6
  toAndFro 3
  take 3
  toAndFro 1
  to 
end
